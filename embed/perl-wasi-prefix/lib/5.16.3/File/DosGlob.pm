#!perl -w

package File::DosGlob;

our $VERSION = '1.06';
use strict;
use warnings;

sub doglob {
    my $cond   = shift;
    my @retval = ();
    my $fix_drive_relative_paths;
  OUTER:
    for my $pat (@_) {
        my @matched  = ();
        my @globdirs = ();
        my $head     = '.';
        my $sepchr   = '/';
        my $tail;
        next OUTER unless defined $pat and $pat ne '';
        if ( $pat =~ /^"(.*)"\z/s ) {
            $pat = $1;
            if   ( $cond eq 'd' ) { push( @retval, $pat ) if -d $pat }
            else                  { push( @retval, $pat ) if -e $pat }
            next OUTER;
        }
        if ( $pat =~ m|^([A-Za-z]:)[^/\\]|s ) {
            substr( $pat, 0, 2 ) = $1 . "./";
            $fix_drive_relative_paths = 1;
        }
        if ( $pat =~ m|^(.*)([\\/])([^\\/]*)\z|s ) {
            ( $head, $sepchr, $tail ) = ( $1, $2, $3 );
            push( @retval, $pat ), next OUTER if $tail eq '';
            if ( $head =~ /[*?]/ ) {
                @globdirs = doglob( 'd', $head );
                push( @retval,
                    doglob( $cond, map { "$_$sepchr$tail" } @globdirs ) ),
                  next OUTER
                  if @globdirs;
            }
            $head .= $sepchr if $head eq '' or $head =~ /^[A-Za-z]:\z/s;
            $pat = $tail;
        }
        unless ( $pat =~ /[*?]/ ) {
            $head = '' if $head eq '.';
            $head .= $sepchr
              unless $head eq ''
              or substr( $head, -1 ) eq $sepchr;
            $head .= $pat;
            if   ( $cond eq 'd' ) { push( @retval, $head ) if -d $head }
            else                  { push( @retval, $head ) if -e $head }
            next OUTER;
        }
        opendir( D, $head ) or next OUTER;
        my @leaves = readdir D;
        closedir D;
        $head = '' if $head eq '.';
        $head .= $sepchr unless $head eq '' or substr( $head, -1 ) eq $sepchr;

        $pat =~ s:([].+^\-\${}()[|]):\\$1:g;
        $pat =~ s/\*/.*/g;
        $pat =~ s/\?/.?/g;

        my $matchsub = sub { $_[0] =~ m|^$pat\z|is };
      INNER:
        for my $e (@leaves) {
            next INNER if $e eq '.' or $e eq '..';
            next INNER if $cond eq 'd' and !-d "$head$e";
            push( @matched, "$head$e" ), next INNER if &$matchsub($e);
            if (    index( $e, '.' ) == -1
                and length($e) < 9
                and index( $pat, '\\.' ) != -1 )
            {
                push( @matched, "$head$e" ), next INNER if &$matchsub("$e.");
            }
        }
        push @retval, @matched if @matched;
    }
    if ($fix_drive_relative_paths) {
        s|^([A-Za-z]:)\./|$1| for @retval;
    }
    return @retval;
}

my %entries;

sub glob {
    my ( $pat, $cxix ) = @_;
    my @pat;

    $pat = $_ unless defined $pat;

    $cxix = '_G_' unless defined $cxix;

    if ( !$entries{$cxix} ) {
        if ( $pat =~ /\s/ ) {
            require Text::ParseWords;
            @pat = Text::ParseWords::parse_line( '\s+', 0, $pat );
        }
        else {
            push @pat, $pat;
        }

      REHASH: {
            my @appendpat = ();
            for (@pat) {
                while (/^(.*)(?<!\\)\{(.*?)(?<!\\)\,.*?(?<!\\)\}(.*)$/) {
                    my ( $start, $match, $end ) = ( $1, $2, $3 );
                    my $tmp = "$start$match$end";
                    while ( $tmp =~
s/^(.*?)(?<!\\)\{(?:.*(?<!\\)\,)?(.*\Q$match\E.*?)(?:(?<!\\)\,.*)?(?<!\\)\}(.*)$/$1$2$3/
                      )
                    {
                    }
                    push @appendpat, ("$tmp");
                    s/^\Q$start\E(?<!\\)\{\Q$match\E(?<!\\)\,/$start\{/;
                    if (
/^\Q$start\E(?<!\\)\{(?!.*?(?<!\\)\,.*?\Q$end\E$)(.*)(?<!\\)\}\Q$end\E$/
                      )
                    {
                        $match = $1;
                        $_     = "$start$match$end";
                    }
                }
            }
            if ( $#appendpat != -1 ) {
                for (@appendpat) {
                    push @pat, $_;
                }
                goto REHASH;
            }
        }
        for (@pat) {
            s/\\{/{/g;
            s/\\}/}/g;
            s/\\,/,/g;
        }

        $entries{$cxix} = [ doglob( 1, @pat ) ];
    }

    if (wantarray) {
        return @{ delete $entries{$cxix} };
    }
    else {
        if ( scalar @{ $entries{$cxix} } ) {
            return shift @{ $entries{$cxix} };
        }
        else {
            delete $entries{$cxix};
            return undef;
        }
    }
}

{
    no strict 'refs';

    sub import {
        my $pkg = shift;
        return unless @_;
        my $sym = shift;
        my $callpkg = ( $sym =~ s/^GLOBAL_//s ? 'CORE::GLOBAL' : caller(0) );
        *{ $callpkg . '::' . $sym } = \&{ $pkg . '::' . $sym }
          if $sym eq 'glob';
    }
}
1;

__END__


